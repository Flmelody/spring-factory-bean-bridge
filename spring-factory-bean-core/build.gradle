plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'org.flmelody'
version = '1.0.0'

java {
    sourceCompatibility = '17'
    withSourcesJar()
    withJavadocJar()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
//    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter:3.1.3'
}

publishing {
    publications {

        mavenJava(MavenPublication) {

            groupId = 'org.flmelody'
            artifactId = 'spring-factory-bean-core'
            version = version
            from components.java

            pom {
                name = 'spring-factory-bean-core'
                description = 'Bridge for outer factory bean and spring container'
                url = 'https://github.com/Flmelody/spring-factory-bean-bridge'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'esotericman'
                        name = 'esotericman'
                    }
                }
                scm {
                    connection = 'scm:git:git:github.com/Flmelody/spring-factory-bean-bridge.git'
                    developerConnection = 'scm:git:ssh://github.com/Flmelody/spring-factory-bean-bridge.git'
                    url = 'https://github.com/Flmelody/spring-factory-bean-bridge.git'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.properties["username"]
                password = project.properties["password"]
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}


